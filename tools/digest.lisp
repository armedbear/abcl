(defvar *digest-types* 
  '((:sha-1 . "SHA-1")
    (:sha-256 . "SHA-256")
    (:sha-512 . "SHA-512")))

(defconstant +byte-buffer-rewind+ 
  (jmethod "java.nio.ByteBuffer" "rewind"))
(defconstant +byte-buffer-get+ 
  (jmethod "java.nio.ByteBuffer" "get" "[B" "int" "int"))
(defconstant +digest-update+ 
  (jmethod "java.security.MessageDigest" "update" "[B" "int" "int"))

;;;  needs ABCL svn > r13328 and is probably not faster than the NIO version

(defun digest-file-1 (path &key (digest :sha-256))
 (let* ((digest-type (cdr (assoc digest *digest-types*)))
        (digest (jstatic "getInstance" "java.security.MessageDigest" digest-type))
        (buffer (make-array 8192 :element-type '(unsigned-byte 8))))
   (with-open-file (input path :element-type '(unsigned-byte 8))
     (loop :for bytes = (read-sequence buffer input)
        :while (plusp bytes)
        :do 
        (jcall-raw "update" digest 
                   (jnew-array-from-array "byte" buffer) 0 bytes))
     (jcall "digest" digest))))

(defun digest-file (path &key (digest :sha-256))
 (let* ((digest-type (cdr (assoc digest *digest-types*)))
        (digest (jstatic "getInstance" "java.security.MessageDigest" digest-type))
        (namestring (if (pathnamep path) (namestring path) path))
        (file-input-stream (jnew "java.io.FileInputStream" namestring))
        (channel (jcall "getChannel" file-input-stream))
        (length 8192)
        (buffer (jstatic "allocateDirect" "java.nio.ByteBuffer" length))
        (array (jnew-array "byte" length)))
   (do ((read (jcall "read" channel buffer)
              (jcall "read" channel buffer)))
       ((not (> read 0)))
     (jcall +byte-buffer-rewind+ buffer)
     (jcall +byte-buffer-get+ buffer array 0 read)
     (jcall +byte-buffer-rewind+ buffer)
     (jcall +digest-update+ digest array 0 read))
   (jcall "digest" digest)))

(defun ascii-digest (digest)
  (format nil "佝磲疸狎灬礅溽ㄢㄩ癌ǐ驳猢猢赆鲠汉扉篝骝镯赆蝌狴溟珏篝┅┅ㄤ彐躅忮钽桧狎ㄤ轵邈麸蝙戾蝈篚祠篝狎舡孱洵篝狎舡孱洵博ㄤ镬轶ㄥ铘蝙ㄤ轵邈麸蝙溟蝈泗矧┅箦翩篝狎舡ㄧ弭轭翦蝾犰蝓瞽糸礤┅ㄤ殓弩舡骈戾孱趄箦翩孱洵ㄧ弭轭翦蝾犰蝓瞽糸礤┅箦翩篝狎舡ㄧ弭轭翦蝾犰蝓瞽糸礤┅ㄤ殓弩舡骈戾孱趄箦翩孱洵ㄧ弭轭翦蝾犰蝓瞽糸礤┅戾è蝈篚祠扉篝孱趄ō孱洵篝狎舡暴ō孱洵篝狎舡博┅ㄦ矧磲立蝈篚祠瘐箬蝈篚祠蝈篚祠螬┅蝈篚祠螬